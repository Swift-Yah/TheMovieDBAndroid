buildscript {
    ext.kotlin_version = '1.1.51'
    ext.android_plugin_version = '3.0.0'

    repositories {
        jcenter()
        google()
        maven { url 'https://jitpack.io' }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$android_plugin_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        jcenter()
        google()
        maven { url 'https://jitpack.io' }
    }
}

subprojects {

    afterEvaluate {

        if (!android) return

        android {
            compileSdkVersion 26
            buildToolsVersion "26.0.2"

            defaultConfig {
                minSdkVersion 21
                targetSdkVersion 26

                versionCode 1
                versionName "1.0"

                resConfigs "pt"
            }

            lintOptions {
                abortOnError false
            }

            aaptOptions {
                cruncherEnabled = false
            }

            testOptions {
                unitTests.all {
                    jacoco {
                        includeNoLocationClasses = true
                    }
                }
                execution 'ANDROID_TEST_ORCHESTRATOR'
                unitTests.returnDefaultValues = true
            }

            packagingOptions {
                exclude 'META-INF/LICENSE'
            }

            sourceSets {
                main.java.srcDirs += 'src/main/kotlin'
                androidTest.java.srcDirs += 'src/androidTest/kotlin'
                test.java.srcDirs += 'src/test/kotlin'
            }

            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }
        }
    }
}

ext {

    // Android
    androidSupport = '27.0.1'
    playServices = '11.6.0'
    constraintLayout = '1.0.2'

    // Google
    gson = '2.8.2'

    // Square
    picasso = '2.5.2'
    okHttp = '3.9.0'
    retrofit = '2.3.0'
    timber = '4.5.1'

    // Android Arch
    arch = '1.0.0'

    // Testing
    kappuccino = '1.0.6'
    espresso = "3.0.1"
    rules = "1.0.1"
    runner = "1.0.1"
    mockito = "2.11.0"
    mockitoKotlin = "1.5.0"
    dexopener = "0.12.0"
    mirror = "1.6.1"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}